// Parsing rules for RPM "Name, Epoch, Version, Release, Architecture" (NEVRA).
// Informal spec: https://fedoraproject.org/wiki/Packaging:Versioning

// Complete NEVRA label.
nevra_input = $ { SOI
              ~ name
              ~ NE_SEPARATOR
              ~ evra
              ~ EOI
              }

// Complete EVRA label.
evra_input  = $ { SOI
              ~ evra
              ~ EOI
              }

// EVRA sub-label.
evra = $ { (epoch ~ EV_SEPARATOR)?
         ~ version
         ~ (VR_SEPARATOR ~ release)?
         ~ (RA_SEPARATOR ~ architecture)?
         }

// NEVRA Fields.
name         = $ { (!NE_SEPARATOR ~ ASCII)+ }
epoch        = $ { (!EV_SEPARATOR ~ ASCII_DIGIT)+ }
version      = $ { (!VR_SEPARATOR ~ CHARSET_VERSION)+ }
release      = $ { (!RA_SEPARATOR ~ ASCII)+ }
architecture = $ { (ASCII)+ }

// NEVRA Tags Charsets.
CHARSET_VERSION = _ { ASCII_ALPHANUMERIC | "." | "^" | "~" }

// NEVRA Separators.
NE_SEPARATOR = _ { "-" }
EV_SEPARATOR = _ { ":" }
VR_SEPARATOR = _ { "-" }
RA_SEPARATOR = _ { "." }

